// THIS MOD CREATED BY heri hover

//00BC GXT 'PROS_01' // "We need to find somewhere more secluded"
//00BC GXT 'PROS_09' // "Stop wasting my time!"
//00BC GXT 'PROS_02' // "This seems an ok place"
// VARIABLES:
// $BET_HKRON: Flag if the mod is on/off
// 0@: Health
// 1@: CarTarget
// 2@: CarSeat
// 3@: HookerTarget
// 4@: PedType
// 5@, 6@, 7@: HookerPosition
// 8@: PedsAroundTargetHooker
// 9@: LoopSpeed
// 10@ 11@ 12@ 13@ 14@ 15@: Timers
// 16@: Bounce direction flip 
// 17@: CarSpeed

{$CLEO}
{$USE CLEO+}

:BET_HKR
0001: wait 1000 ms
00D6: if
$BET_HKRON == 0
then
    03A4: name_thread 'BTHKMTR' 
    :BTHKMTR
    0001: wait 1000 ms
    //03F0: enable_text_draw 1
    //045A: draw_text_1number 200.0 40.0 GXT 'NUMBER' number $BET_HKRON
    00D6: if
    $BET_HKRON == 0
    then
        //03E5: show_text_highpriority GXT 'CLOTHC' time 5000 flag 1
        $BET_HKRON = 1 // mark as running
        0A92: create_custom_thread "GTA_SA_BETA_HOOKER.cs"
    end
    jump @BTHKMTR
else
jump @BET_HKR0
end

:BET_HKR0
0001: wait 0 ms
00D6: if
$BET_HKRON == 0
then
0459: terminate_all_scripts_with_this_name 'BET_HKR'
else
03A4: name_thread 'BET_HKR'
:BET_HKR1
0001: wait 1000 ms
//03F0: enable_text_draw 1
//045A: draw_text_1number 200.0 100.0 GXT 'NUMBER' number $BET_HKRON
04ED: load_animation "BLOWJOBZ" 
04ED: load_animation "SEX_CAR" 
04ED: load_animation "CAR_CHAT" 
038B: load_requested_models
0001: wait 0 ms  
var            
    0@: Integer
    9@: Integer
    10@: Integer
    11@: Integer
    12@: Integer
    13@: Integer
    14@: Integer
    15@: Integer
    16@: Integer
end     
gosub @RESET
jump @INIT
end 

:INIT 
0001: wait 0 ms
00D6: if 
Actor.Driving($PLAYER_ACTOR) 
else_jump @INIT    
00D6: if and
Player.Defined($PLAYER_CHAR)
$ONMISSION == 0 
847A:   not actor $PLAYER_ACTOR driving_bike 
84A7:   not actor $PLAYER_ACTOR driving_boat 
84C8:   not actor $PLAYER_ACTOR driving_flying_vehicle 
84A9:   not actor $PLAYER_ACTOR driving_heli 
else_jump @INIT    
1@ = Actor.CurrentCar($PLAYER_ACTOR)
01EA: 2@ = car 1@ max_passengers
00D6: if 
2@ > 0
else_jump @INIT        
01E9: 2@ = car 1@ num_passengers
00D6: if 
2@ > 0
else_jump @INIT
00D6: if 
8431:   not car 1@ passenger_seat_free 0  
else_jump @INIT
0432: 3@ = get_actor_handle_from_car 1@ passenger_seat 0 
00D6: if 
056D:   does_char_exist 3@
else_jump @INIT
00D6: if or
02F2:   actor 3@ model == #BFYPRO
02F2:   actor 3@ model == #HFYPRO 
02F2:   actor 3@ model == #WFYPRO 
02F2:   actor 3@ model == #SFYPRO
then
jump @FOREPLAY
else
00D6: if or
02F2:   actor 3@ model == #SWFOPRO
02F2:   actor 3@ model == #SBFYPRO
02F2:   actor 3@ model == #SHFYPRO
02F2:   actor 3@ model == #VWFYPRO
02F2:   actor 3@ model == #VBFYPRO
02F2:   actor 3@ model == #VHFYPRO
else_jump @INIT
jump @FOREPLAY 
end

:FOREPLAY
0001: wait 0 ms
gosub @WAIT_1000
0605: As_actor 3@ perform_animation "CAR_SC1_FR" IFP "CAR_CHAT" framedelta 4.0 loop -1 lockX 0 lockY 0 lockF 0 time -1
03FE: set_actor 3@ money 5
Player.Money($PLAYER_CHAR) += -5
0624: add 5.0 to_float_stat 33
gosub @WAIT_1000 
0605: As_actor 3@ perform_animation "BJ_CAR_START_W" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1
gosub @WAIT_500 
0812: AS_actor 3@ perform_animation "BJ_CAR_LOOP_W" IFP "BLOWJOBZ" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1
jump @WATCH_FP

:WATCH_FP
0001: wait 0 ms
gosub @WAIT_1000 
0812: AS_actor 3@ perform_animation "BJ_CAR_LOOP_W" IFP "BLOWJOBZ" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1
00D6: if
    01C1:   is_car_stopped 1@
then 
    gosub @WAIT_1000 
    Actor.StorePos(3@, 5@, 6@, 7@)
    073F: get_actor_in_sphere 5@ 6@ 7@ radius 18.0 with_pedtype_civilian 1 gang 1 criminal/prostitute 0 handle_as 8@
    00D6: if and
    8@ < 1 
    01C1:   is_car_stopped 1@ 
    then
        00BC: show_text_highpriority GXT 'PROS_02' time 4000 flag 1 // "This seems an ok place"
        03C8: set_camera_directly_before_player
        gosub @JACKED_FP
        jump @BANGBANG
    else
        00BC: show_text_highpriority GXT 'PROS_01' time 4000 flag 1 // "We need to find somewhere more secluded"
        jump @WATCH_FP
    end
else 
    jump @WATCH_FP
end

:JACKED_FP 
0001: wait 0 ms 
gosub @LOCK_CJ
0605: As_actor $PLAYER_ACTOR perform_animation "BJ_CAR_END_P" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1     
0605: actor 3@ perform_animation "BJ_CAR_END_W" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
gosub @WAIT_500 
gosub @HEALTH_UP
gosub @UNLOCK_CJ
return

:BANGBANG 
0001: wait 0 ms
gosub @LOCK_CJ
03FE: set_actor 3@ money 10
Player.Money($PLAYER_CHAR) += -10
0624: add 10.0 to_float_stat 33
0605: As_actor $PLAYER_ACTOR perform_animation "BJ_CAR_START_P" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1  
0605: As_actor 3@ perform_animation "SEX_CAR_START_W" IFP "SEX_CAR" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1
gosub @WAIT_300 
gosub @WAIT_300
0812: AS_actor 3@ perform_animation "SEX_CAR_LOOP_W" IFP "SEX_CAR" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1 
10@ = 900
16@ = 0
9@ = 30
gosub @UNLOCK_CJ 
00A9: car_set_idle 1@  
jump @WATCH_BB

:WATCH_BB
0001: wait 0 ms
//0EA5: 18@ = get_closest_cop_near_char $PLAYER_ACTOR radius 18.0 alive 1 inCar 1 onFoot 1 seenInFront 1
//00D6: if
//056D:   does_char_exist 18@
//then  
//09B6: set_char_wanted_by_police $PLAYER_CHAR to 1
//09B6: set_char_wanted_by_police 5@ to 1
//end
00D6: if and 
10@ < 750
10@ >= 400
then
9@ = 20
end
00D6: if 
10@ < 400
then
9@ = 10
end
0812: AS_actor 3@ perform_animation "SEX_CAR_LOOP_W" IFP "SEX_CAR" framedelta 1.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1 
gosub @BOUNCE
gosub @WAIT_BB
gosub @DEFINED
gosub @BOUNCE
gosub @WAIT_BB
gosub @NOT_DEAD
gosub @BOUNCE
gosub @WAIT_BB
gosub @IN_CAR
Dec(10@,9@)
02E3: 17@ = get_car_speed 1@
00D6: if
17@ > 1075000000 
then
    10@ = 0
    jump @WASTING
else
    00D6: if 
    10@ <= 0 
    then
        gosub @JACKED_BB
        jump @ENDED
    else     
        jump @WATCH_BB
    end 
end

:BOUNCE
0001: wait 0 ms
1@ = Actor.CurrentCar($PLAYER_ACTOR)
//097A: play_audio_at $TEMPVAR_X_COORD $TEMPVAR_Y_COORD $TEMPVAR_Z_COORD event 132032
//018D: NOP $TEMPVAR_X_COORD $TEMPVAR_Y_COORD $TEMPVAR_Z_COORD 132032 20@
//playSFX("GENRL", 132, 032, true)
05C1: AS_actor 3@ speak_from_audio_table 116 
00D6: if 
16@ == 0
then
07DA: set_car 1@ rotation_velocity_XYZ 0.0 -0.35 0.0 through_center_of_body
    00D6: if 
    10@ < 550
    then
    07D5: set_car 1@ velocity_in_direction_XYZ 0.0 0.0 -0.025 rotation_velocitiesXY 0.0 0.0 unk 0.0 
    end 
01C3: mark_car_as_no_longer_needed 1@
16@ = 1
else
07DA: set_car 1@ rotation_velocity_XYZ 0.0 0.35 0.0 through_center_of_body
    00D6: if 
    10@ < 550
    then
    07D5: set_car 1@ velocity_in_direction_XYZ 0.0 0.0 -0.025 rotation_velocitiesXY 0.0 0.0 unk 0.0 
    end 
01C3: mark_car_as_no_longer_needed 1@
16@ = 0
end
1@ = Actor.CurrentCar($PLAYER_ACTOR)
return

:JACKED_BB
0001: wait 0 ms
gosub @LOCK_CJ
15@ = 300
repeat
00D6: if 
15@ > 0
then
    gosub @BOUNCE
    0001: wait 10 ms
    Dec(15@,10)
else
    break
end
until 15@ <= 0
0605: As_actor $PLAYER_ACTOR perform_animation "BJ_CAR_END_P" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
0605: As_actor 3@ perform_animation "SEX_CAR_END_W" IFP "SEX_CAR" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
gosub @WAIT_500
gosub @HEALTH_UP
gosub @WAIT_500 
gosub @UNLOCK_CJ
return

:HEALTH_UP
0001: wait 0 ms
0@ = Actor.Health($PLAYER_ACTOR)
Actor.Health($PLAYER_ACTOR) = 0@ + 25
return

:WASTING
0001: wait 0 ms
00BC: show_text_highpriority GXT 'PROS_09' time 4000 flag 1 // "Stop wasting my time!"
jump @ENDED


:DEFINED
0001: wait 0 ms
00D6: if 
056D:   does_char_exist 3@
then
return
else
jump @TERMINATE
end

:NOT_DEAD
0001: wait 0 ms 
00D6: if or
0117: is_player_dead $PLAYER_CHAR
0119: is_car_dead 1@     
then
    00D6: if
    0119: is_car_dead 1@
    then
    05BE: task_die 3@
    jump @TERMINATE
    else
    jump @ENDED
    end
else
return
end

:IN_CAR // CJ Being jacked, busted, or exits
0001: wait 0 ms 
00D6: if and 
00DB: actor $PLAYER_ACTOR in_car 1@  
00DB: actor 3@ in_car 1@
then
    00D6: if or
    0741: actor $PLAYER_ACTOR busted 
    0E42: is_char_doing_task_id $PLAYER_ACTOR 818 //TASK_SIMPLE_CAR_QUICK_BE_DRAGGED_OUT
    0E42: is_char_doing_task_id $PLAYER_ACTOR 821 //TASK_SIMPLE_CAR_SLOW_BE_DRAGGED_OUT
    0E42: is_char_doing_task_id $PLAYER_ACTOR 823 //TASK_COMPLEX_CAR_SLOW_BE_DRAGGED_OUT
    0E42: is_char_doing_task_id $PLAYER_ACTOR 825 //TASK_COMPLEX_CAR_QUICK_BE_DRAGGED_OUT
    then
        jump @ENDED     
    else
        00D6: if
        0E4A: is_char_exiting_any_car $PLAYER_ACTOR
        then
        jump @WASTING
        else
        return
        end 
    end  
else
jump @ENDED
end

:LOCK_CJ
0001: wait 0 ms 
07CC: set_player $PLAYER_CHAR can_enter_exit_vehicles 0
0221: set_player $PLAYER_CHAR apply_brakes_to_car 1  
return

:UNLOCK_CJ
0001: wait 0 ms 
07CC: set_player $PLAYER_CHAR can_enter_exit_vehicles 1
0221: set_player $PLAYER_CHAR apply_brakes_to_car 0  
return

:CHECK
0001: wait 0 ms
gosub @DEFINED
gosub @NOT_DEAD
gosub @IN_CAR
return

:WAIT_300
0001: wait 0 ms
11@ = 300
repeat
00D6: if 
11@ > 0
then
    Dec(11@,100)
    gosub @CHECK
    0001: wait 100 ms
else
    break
end
until 11@ <= 0
return

:WAIT_500
0001: wait 0 ms
12@ = 500
repeat
00D6: if 
12@ > 0
then
    Dec(12@,100)
    gosub @CHECK
    0001: wait 100 ms
else
    break
end
until 12@ <= 0
return

:WAIT_1000
0001: wait 0 ms
13@ = 1000
repeat
00D6: if 
13@ > 0
then
    Dec(13@,100)
    gosub @CHECK
    0001: wait 100 ms
else
    break
end
until 13@ <= 0
return

:WAIT_BB
0001: wait 0 ms
14@ = 9@ * 1
repeat
00D6: if 
14@ > 0
then
    Dec(14@,10)
    0001: wait 10 ms
else
    break
end
until 14@ <= 0
return 

:ENDED
0001: wait 0 ms 
1@ = Actor.CurrentCar($PLAYER_ACTOR)
00D6: if and
056E: car 1@ defined
00DB: actor 3@ in_car 1@
then
gosub @RESET
0633: task_leave_any_car 3@
05DE: AS_actor 3@ walk_around_ped_path
end
jump @TERMINATE

:TERMINATE
0001: wait 0 ms
gosub @UNLOCK_CJ
gosub @RESET 
00D6: if 
056D: actor 3@ defined
then
    0687: clear_actor 3@ task
    01C2: mark_char_as_no_longer_needed 3@
end
$BET_HKRON = 0
jump @BET_HKR0

:RESET
0001: wait 0 ms
// Reset Timers
9@ = 0
10@ = 0
11@ = 0
12@ = 0
13@ = 0
14@ = 0
15@ = 0 
return
