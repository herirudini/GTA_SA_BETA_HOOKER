// THIS MOD CREATED BY heri hover

//00BC GXT 'PROS_01' // "We need to find somewhere more secluded"
//00BC GXT 'PROS_09' // "Stop wasting my time!"
//00BC GXT 'PROS_02' // "This seems an ok place"
// VARIABLES:
// 0@: Health
// 1@: CarTarget
// 2@: CarSeat
// 3@: HookerTarget
// 4@: PedType
// 5@, 6@, 7@: HookerPosition
// 8@: PedsAroundTargetHooker
// 9@: LoopSpeed
// 10@ 11@ 12@ 13@ 14@: Timers
// 15@ 16@ 17@ 18@: RandomNumber

{$CLEO .cs}
03A4: name_thread 'BETA_HOOKER'

:BETA_HOOKER
0001: wait 0 ms
04ED: load_animation "BLOWJOBZ" 
04ED: load_animation "SEX_CAR" 
04ED: load_animation "CAR_CHAT" 
038B: load_requested_models
0001: wait 0 ms  
var            
    0@: Integer
    9@: Integer
    10@: Integer
    11@: Integer
    12@: Integer
    13@: Integer
    14@: Integer
    15@: Integer
    16@: Integer
    17@: Integer
    18@: Integer
end     
gosub @RESET
jump @INIT 

:INIT 
0001: wait 0 ms
if 
00DF:   actor $PLAYER_ACTOR driving 
else_jump @INIT    
if and
Player.Defined($PLAYER_CHAR)
$ONMISSION == 0 
847A:   not actor $PLAYER_ACTOR driving_bike 
84A7:   not actor $PLAYER_ACTOR driving_boat 
84C8:   not actor $PLAYER_ACTOR driving_flying_vehicle 
84A9:   not actor $PLAYER_ACTOR driving_heli 
else_jump @INIT    
1@ = Actor.CurrentCar($PLAYER_ACTOR)
01EA: 2@ = car 1@ max_passengers
if 2@ > 0
else_jump @INIT        
01E9: 2@ = car 1@ num_passengers
if 2@ > 0
else_jump @INIT
if 8431:   not car 1@ passenger_seat_free 0  
else_jump @INIT
0432: 3@ = get_actor_handle_from_car 1@ passenger_seat 0 
if 3@ > -1
else_jump @INIT
//089F: get_actor 3@ pedtype_to 4@
//if 4@ == 22 
00D6: if or
02F2:   actor 3@ model == #BFYPRO
02F2:   actor 3@ model == #HFYPRO 
02F2:   actor 3@ model == #WFYPRO 
02F2:   actor 3@ model == #SFYPRO
then
jump @PREPARE 
else
00D6: if or
02F2:   actor 3@ model == #SWFOPRO
02F2:   actor 3@ model == #SBFYPRO
02F2:   actor 3@ model == #SHFYPRO
02F2:   actor 3@ model == #VWFYPRO
02F2:   actor 3@ model == #VBFYPRO
02F2:   actor 3@ model == #VHFYPRO
else_jump @INIT
jump @PREPARE 
end

:PREPARE
0001: wait 0 ms
0605: As_actor 3@ perform_animation "CAR_SC1_FR" IFP "CAR_CHAT" framedelta 4.0 loop -1 lockX 0 lockY 0 lockF 0 time -1
gosub @LOCK 
jump @FOREPLAY

:FOREPLAY
0001: wait 0 ms
03FE: set_actor 3@ money 5
Player.Money($PLAYER_CHAR) += -5
0624: add 5.0 to_float_stat 33
gosub @WAIT_1000 
0605: As_actor 3@ perform_animation "BJ_CAR_START_W" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1
gosub @WAIT_500 
0812: AS_actor 3@ perform_animation "BJ_CAR_LOOP_W" IFP "BLOWJOBZ" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1
jump @WATCH_FP

:WATCH_FP
0001: wait 0 ms
gosub @WAIT_1000 
0812: AS_actor 3@ perform_animation "BJ_CAR_LOOP_W" IFP "BLOWJOBZ" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1
if
    01C1:   is_car_stopped 1@
then 
    gosub @WAIT_1000 
    Actor.StorePos(3@, 5@, 6@, 7@)
    073F: get_actor_in_sphere 5@ 6@ 7@ radius 18.0 with_pedtype_civilian 1 gang 1 criminal/prostitute 0 handle_as 8@
    if and
    8@ < 1 
    01C1:   is_car_stopped 1@ 
    then
        00BC: show_text_highpriority GXT 'PROS_02' time 4000 flag 1 // "This seems an ok place"
        03C8: set_camera_directly_before_player
        gosub @JACKED_FP
        jump @BANGBANG
    else
        00BC: show_text_highpriority GXT 'PROS_01' time 4000 flag 1 // "We need to find somewhere more secluded"
        jump @WATCH_FP
    end
else 
    jump @WATCH_FP
end

:JACKED_FP 
0001: wait 0 ms 
0221: set_player $PLAYER_CHAR apply_brakes_to_car 1
0605: As_actor $PLAYER_ACTOR perform_animation "BJ_CAR_END_P" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1     
0605: actor 3@ perform_animation "BJ_CAR_END_W" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
gosub @WAIT_500 
gosub @HEALTH_UP
0221: set_player $PLAYER_CHAR apply_brakes_to_car 0
return

:BANGBANG 
0001: wait 0 ms
03FE: set_actor 3@ money 10
Player.Money($PLAYER_CHAR) += -10
0624: add 10.0 to_float_stat 33
0605: As_actor $PLAYER_ACTOR perform_animation "BJ_CAR_START_P" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1  
0605: As_actor 3@ perform_animation "SEX_CAR_START_W" IFP "SEX_CAR" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1
gosub @WAIT_300 
gosub @WAIT_300
0812: AS_actor 3@ perform_animation "SEX_CAR_LOOP_W" IFP "SEX_CAR" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1 
10@ = 6000
15@ = 0
9@ = 50
16@ = 0.06
17@ = 0.02
18@ = -0.02
0221: set_player $PLAYER_CHAR apply_brakes_to_car 1
jump @WATCH_BB

:WATCH_BB
0001: wait 0 ms
if 10@ < 3000
then
9@ = 25 
16@ = 0.05
17@ = 0.025
18@ = -0.025
end
if 10@ < 1000
then
9@ = 25
16@ = 0.06
17@ = 0.025
18@ = -0.025
end
0812: AS_actor 3@ perform_animation "SEX_CAR_LOOP_W" IFP "SEX_CAR" framedelta 1.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1 
gosub @BOUNCE
gosub @WAIT_BB
05C1: AS_actor 3@ speak_from_audio_table 116
10@ = 10@ - 9@
00D6: if or
00E1:   player 0 pressed_key 14 // Acelerate
00E1:   player 0 pressed_key 16 // Brake/Reverse
then
    10@ = 0
    0221: set_player $PLAYER_CHAR apply_brakes_to_car 0
    jump @WASTING
else
    if 10@ <= 0 
    then
        gosub @JACKED_GG
        0221: set_player $PLAYER_CHAR apply_brakes_to_car 0
        jump @ENDED
    else     
        jump @WATCH_BB
    end
end

:BOUNCE
0001: wait 0 ms
1@ = Actor.CurrentCar($PLAYER_ACTOR)
// apply bouncing 
if 15@ == 0
then
07D5: set_car 1@ velocity_in_direction_XYZ 17@ 18@ 16@ rotation_velocitiesXY 0.02 0.02 unk 0.0  
01C3: remove_references_to_car 1@
15@ = 1
else
07D5: set_car 1@ velocity_in_direction_XYZ 18@ 17@ 16@ rotation_velocitiesXY -0.02 -0.02 unk 0.0
01C3: remove_references_to_car 1@
15@ = 0
end
1@ = Actor.CurrentCar($PLAYER_ACTOR)
return

:JACKED_GG
0001: wait 0 ms
0221: set_player $PLAYER_CHAR apply_brakes_to_car 1
0605: As_actor $PLAYER_ACTOR perform_animation "BJ_CAR_END_P" IFP "BLOWJOBZ" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
0605: As_actor 3@ perform_animation "SEX_CAR_END_W" IFP "SEX_CAR" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
gosub @WAIT_500
gosub @HEALTH_UP
gosub @WAIT_500 
0221: set_player $PLAYER_CHAR apply_brakes_to_car 0
return

:HEALTH_UP
0001: wait 0 ms
0@ = Actor.Health($PLAYER_ACTOR)
0@ += 25 
Actor.Health($PLAYER_ACTOR) = 0@
return

:WASTING
0001: wait 0 ms
00BC: show_text_highpriority GXT 'PROS_09' time 4000 flag 1 // "Stop wasting my time!"
jump @ENDED


:DEFINED
0001: wait 0 ms
if 056D:   does_char_exist 3@
then
return
else
jump @TERMINATE
end

:NOT_DEAD
0001: wait 0 ms 
00D6: if or
0117: is_player_dead $PLAYER_CHAR
0119: is_car_dead 1@     
then
05BE: task_die 3@
0001: wait 100 ms
034F: remove_char_elegantly 3@
jump @TERMINATE
else
return
end

:IN_CAR
0001: wait 0 ms 
if and
00DB: actor $PLAYER_ACTOR in_car 1@  
00DB: actor 3@ in_car 1@
then 
    if
    00E1: player 0 pressed_key 15 // Exit 
    then
    jump @WASTING
    else
        00D6: if or // CJ Being jacked, busted, or car front door breaks
        08A7:   is_car_door_fully_open 1@ door 2
        0741:   actor $PLAYER_ACTOR busted
        then
        jump @ENDED
        else
        return  
        end     
    end
else
jump @ENDED
end

:LOCK
0001: wait 0 ms 
0526: set_char_stay_in_car_when_jacked 3@ to 1   
return

:WAIT_300
0001: wait 0 ms
11@ = 300
repeat
11@ -= 100
gosub @DEFINED
gosub @NOT_DEAD
gosub @IN_CAR
gosub @LOCK
0001: wait 100 ms
until 11@ <= 0
return

:WAIT_500
0001: wait 0 ms
12@ = 500
repeat
12@ -= 100
gosub @DEFINED
gosub @NOT_DEAD
gosub @IN_CAR
gosub @LOCK
0001: wait 100 ms
until 12@ <= 0
return

:WAIT_1000
0001: wait 0 ms
13@ = 1000
repeat
13@ -= 100
gosub @DEFINED
gosub @NOT_DEAD
gosub @IN_CAR
gosub @LOCK
0001: wait 100 ms
until 13@ <= 0
return

:WAIT_BB
0001: wait 0 ms
14@ = 0
repeat
14@ += 25
gosub @DEFINED
gosub @NOT_DEAD
gosub @IN_CAR
gosub @LOCK
00D6: if or
00E1:   player 0 pressed_key 14 // Acelerate
00E1:   player 0 pressed_key 16 // Brake/Reverse
then
    0221: set_player $PLAYER_CHAR apply_brakes_to_car 0 
end
0001: wait 25 ms
until 14@ >= 9@
return 

:ENDED
0001: wait 0 ms
if 
8431: not car 1@ passenger_seat_free 0
then
0526: set_char_stay_in_car_when_jacked 3@ to 0   
gosub @RESET
0633: AS_actor 3@ exit_car
05DE: AS_actor 3@ walk_around_ped_path
jump @ENDED
else
jump @TERMINATE
end

:TERMINATE
0001: wait 0 ms
gosub @RESET 
0221: set_player $PLAYER_CHAR apply_brakes_to_car 0
0687: clear_actor $PLAYER_ACTOR task
if
1@ > -1
then
07D5: set_car 1@ velocity_in_direction_XYZ 0.0 0.0 0.0 rotation_velocitiesXY 0.0 0.0 unk 0.0
01C3: mark_car_as_no_longer_needed 1@
1@ = -1
end
if 
3@ > -1
then
0687: clear_actor 3@ task
01C2: mark_char_as_no_longer_needed 3@
3@ = -1
end
00D6: if or
1@ > -1
3@ > -1
then
jump @TERMINATE
else 
0001: wait 1000 ms
jump @INIT
end

:RESET
0001: wait 0 ms
//0@ = 0
//1@ = -1
//2@ = 0
//3@ = -1
//4@ = 0
//5@ = 0
//6@ = 0
//7@ = 0
// STOP Timers
9@ = 0
10@ = 0
11@ = 0
12@ = 0
13@ = 0
14@ = 0
// RESET Bounce effect
15@ = 0
16@ = 0 
17@ = 0 
18@ = 0 
00D6: if or
9@ > 0
10@ > 0
11@ > 0
12@ > 0
13@ > 0
14@ > 0
then
jump @RESET
else
return    
end
